#!/usr/local/bin/gnuplot
#
# Creates a version of a plot, which looks nice for inclusion on web pages
#
# AUTHOR: Hagen Wierstorf

reset

set terminal pngcairo size 800,400 enhanced font 'Verdana,9'
set output 'running_time.png'

# define axis
# remove border on top and right and set color to gray
set style line 11 lc rgb '#808080' lt 1
set border 3 back ls 11
set tics nomirror
# define grid
set style line 12 lc rgb '#808080' lt 0 lw 1
set grid back ls 12

# color definitions
set style line 1 lc rgb '#3B8686' pt 1 ps 1 lt 1 lw 2
set style line 2 lc rgb '#D95B43' pt 6 ps 1 lt 1 lw 2
set style line 3 lc rgb '#8FBE00' pt 2 ps 1 lt 1 lw 2

set key bottom right

set xlabel 'Word count'
set ylabel 'Running time (s)'

plot '../lexique_tableau.txt' u 1:2 t 'Array' w lp ls 1, \
     '../lexique_liste.txt'   u 1:2 t 'Linked list' w lp ls 2, \
     '../lexique_hash.txt'    u 1:2 t 'Hash table' w lp ls 3